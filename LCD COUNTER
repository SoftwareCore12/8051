;8051 to LCD connection
;connect P0.0 to D0 , P0.1 to D2 ,-------,P0.7 to D7 . P2.0 to RS, P2.1 to R/W, P2.2 to E
;connect square wave generator to P3.4. set SWG to 1hz freq. or any freq.
ORG 0H
	ACALL LCD_SET_UP        ;lcd setup subroutine
	MOV TMOD,#00000110B     ;timer 0 , 8-bit auto reload
	MOV TH0,#00H            
	SETB P3.4               ;make p3.4 an input pin
	AGAIN: SETB TR0         ; start timer
	BACK: MOV A,TL0         ;move timer value to accumulator
	ACALL CONV              ;call binary to ascii conversion subroutine
	ACALL DISPLAY           ;display count data on lcd
	JNB TF0,AGAIN           ;wait for time flag to go high
	CLR TR0                 ;stop timer
	CLR TF0                 ;reset timer flag
	SJMP AGAIN              ;stay here
	
	CONV:
	MOV B,#10               ;divide by 10
	DIV AB
	MOV R2,B                ;save low digit in R2
	MOV B,#10               ;divide by 10 once more
	DIV AB            
	ORL A,#30H              ;make it ascii
	MOV R4,A                ;save MSD
	MOV A,B
	ORL A,#30H              ;make 2nd digit an ascii
	MOV R3,A                ;save it in R3
	MOV A,R2                
	ORL A,#30H              ;make 3rd digit an ascii
	MOV R2,A                ;save it in R2
	RET
	
	LCD_SET_UP:             ;lcd setup subroutine
	MOV A,#38H              ;init lcd 2-lines 5x7 matrix
	ACALL COMNWRT           ;call command write subroutine
	ACALL DELAY             
	MOV A,#0CH              ;display on,cursor off
	ACALL COMNWRT
	ACALL DELAY
	MOV A,#01H              ;clear display screen
	ACALL COMNWRT
	ACALL DELAY
	MOV A,#80H              ;force cursor to line 1 pos 1
	ACALL COMNWRT
	ACALL DELAY
	RET
	
	DISPLAY:
	MOV A,R4          ;move msd to pos 1
	ACALL DATAWRT
	ACALL DELAY
	MOV A,R3          ;move 2nd digit to pos 2
	ACALL DATAWRT
	ACALL DELAY
	MOV A,R2          ;move lsd to pos 3
	ACALL DATAWRT
	ACALL DELAY
	MOV A,#80H        ;reset lcd to line 1 pos 1
	ACALL COMNWRT
	ACALL DELAY
	RET
	
	COMNWRT:
	MOV P0,A          ;move data to port0
	CLR P2.0          
	CLR P2.1
	SETB P2.2
	ACALL DELAY
	CLR P2.2
	RET
	
	DATAWRT:
	MOV P0,A
	SETB P2.0
	CLR P2.1
	SETB P2.2
	ACALL DELAY
	CLR P2.2
	RET
	
	DELAY:MOV R5,#50        ; small delay
	HERE2: MOV R6,#255
	HERE: DJNZ R6,HERE
	DJNZ R5,HERE2
	RET
	
	END
